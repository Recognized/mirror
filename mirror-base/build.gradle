
plugins {
  id "java"
  id "eclipse"
  id "idea"
  id "com.google.protobuf"
  id 'com.github.johnrengelman.shadow'
  id "com.github.ben-manes.versions"
  id "com.github.breadmoirai.github-release"
}

def grpcVersion = '1.35.0'
// This version must match the protobuf-java version that grpc transitively pulls in
def protobufVersion = '3.6.1'

version = 'git describe --tags --dirty'.execute().text.trim()

dependencies {
  implementation "io.grpc:grpc-netty:$grpcVersion"
  implementation "io.grpc:grpc-protobuf:$grpcVersion"
  implementation "io.grpc:grpc-stub:$grpcVersion"
  implementation "javax.annotation:javax.annotation-api:1.3.2"
  implementation "ch.qos.logback:logback-classic:1.2.2"
  implementation "org.slf4j:jul-to-slf4j:1.7.25"
  implementation 'com.github.jnr:jnr-posix:3.0.49'
  implementation 'com.github.stephenh:watchman:d01789af5df'
  implementation 'org.eclipse.jgit:org.eclipse.jgit:5.2.1.201812262042-r'
  implementation 'commons-io:commons-io:2.6'
  implementation 'org.apache.commons:commons-lang3:3.8.1'
  // Keep on 0.9.10 as it doesn't matter and 0.9.14 requires newer JDKs
  implementation 'org.jooq:jool:0.9.10'
  implementation 'com.github.rvesse:airline:2.6.0'
  implementation 'com.github.stephenh.joist:joist-util:1.14.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
  testImplementation 'org.mockito:mockito-core:2.24.5'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

jar {
   manifest {
       attributes 'Mirror-Version': version
   }
}

shadowJar {
  archiveVersion = ''
}

eclipse {
  pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
  pathVariables 'M2_REPO': new File("${System.getenv('HOME')}/.m2/repository")
  classpath {
    downloadSources = true
  }
}

githubRelease {
  token findProperty('github.token') ?: 'unset'
  owner 'stephenh'
  tagName version
  releaseAssets.from('build/libs/mirror-all.jar', './mirror')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// E.g. '9.0.1 (Oracle Corporation 9.0.1+11)' or '1.8.0_151 (Oracle Corporation 25.151-b12)'
// or '11 (Oracle Corporation 11+24-Ubuntu-118.04)'
def currentJvm = org.gradle.internal.jvm.Jvm.current()
def jvmVersion = (currentJvm.toString() =~ /^\d+/)[0].toDouble()
if (jvmVersion >= 1.9) {
  compileJava {
    options.compilerArgs.addAll(['--release', '8'])
  }
}

